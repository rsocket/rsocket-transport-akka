plugins {
    id 'scala'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.7.3'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.adtran.scala-multiversion-plugin' version '1.0.32'
}

group = 'io.rsocket'
description = 'Akka RSocket transport implementations (TCP, Websocket)'

repositories {
    jcenter()
}

ext {
    rsocketVersion    = "0.11.12"
    akkaHttpVersion   = "10.1.5"
    akkaStreamVersion = "2.5.17"
    junitVersion      = "5.1.0"
}

dependencies {
    zinc "com.typesafe.zinc:zinc:0.3.15"

    compile "org.scala-lang:scala-library:%scala-version%"
    compile "io.rsocket:rsocket-core:$rsocketVersion"
    compile "com.typesafe.akka:akka-http_%%:$akkaHttpVersion"
    compile "com.typesafe.akka:akka-stream_%%:$akkaStreamVersion"

    testCompile "io.rsocket:rsocket-test:$rsocketVersion"
    testCompile "io.rsocket:rsocket-transport-netty:$rsocketVersion"
    testCompile "com.typesafe.akka:akka-actor-testkit-typed_%%:$akkaStreamVersion"

    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    testRuntimeOnly "com.typesafe.akka:akka-slf4j_%%:$akkaStreamVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'javadoc'
    from scaladoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives scaladocJar
}

plugins.withType(MavenPublishPlugin) {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId archivesBaseName

                from components.java

                artifact sourcesJar
                artifact scaladocJar

                pom.withXml {
                    asNode().':version' + {
                        resolveStrategy = DELEGATE_FIRST

                        name project.name
                        description project.description
                        url 'http://rsocket.io'

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            }
                        }

                        developers {
                            developer {
                                id 'rdegnan'
                                name 'Ryland Degnan'
                                email 'ryland@netifi.com'
                            }
                        }

                        scm {
                            connection 'scm:git:git@github.com:rsocket/rsocket-transport-akka.git'
                            developerConnection 'scm:git:git@github.com:rsocket/rsocket-transport-akka.git'
                            url 'https://github.com/rsocket/rsocket-transport-akka'
                        }
                    }
                }
            }
        }
    }
}

apply from: 'artifactory.gradle'
apply from: 'bintray.gradle'

ScalaCompileOptions.metaClass.daemonServer = true
ScalaCompileOptions.metaClass.fork = true
ScalaCompileOptions.metaClass.useAnt = false
ScalaCompileOptions.metaClass.useCompileDaemon = false

project.tasks.scaladoc.scalaDocOptions.additionalParameters = ["-no-link-warnings"]
project.tasks.compileScala.scalaCompileOptions.additionalParameters = ["-target:jvm-1.8"]
project.tasks.compileTestScala.scalaCompileOptions.additionalParameters = ["-target:jvm-1.8"]

test {
    useJUnitPlatform()

    maxParallelForks = 1
}
